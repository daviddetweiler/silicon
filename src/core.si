header: [ dictionary store immediate
ptr-invoke-thread 	assemble
find: 0				assemble
find: is-assembling assemble
find: store			assemble
find: return		assemble

header: ] dictionary store
ptr-invoke-thread assemble
-1 is-assembling store
	-1 is-assembling store
	return
[

header: assemble-invoke-thread dictionary store
ptr-invoke-thread assemble
]
	ptr-invoke-thread assemble
	return
[

header: assemble: dictionary store immediate
assemble-invoke-thread
] find: assemble return [

header: \ dictionary store immediate
assemble-invoke-thread
] flush-line return [

\ `define:` acts to create a new word at the tip of the dictionary, with subsequent `assemble` instructions
\ appending to its definition
header: define: dictionary store
assemble-invoke-thread
] header: dictionary store return [

\ `end-fn` terminates the current definition with a `return`, and disables assembly mode in the interpreter
define: end-fn immediate
assemble-invoke-thread
]
	current-definition
	load
	dictionary
	store
	0
	current-definition
	store
	literal
	return
	assemble
	assemble: [
	return
[

\ `fn:` defines a new word, specifies it as a thread, and enables assembly mode in the interpreter
define: fn:
assemble-invoke-thread
]
	]
	header: current-definition store
	assemble-invoke-thread
	return
[

fn: high-level:
	\ --

	define:
	ptr-invoke-high-level assemble
	0 assemble
end-fn

fn: patch-high-level
	\ reference-ptr --
	dictionary load
	entry-data-ptr cell-size +
	store
end-fn

fn: does>
	\ --
	literal literal						assemble
	assembly-arena-ptr cell-size 3 * +	assemble \ Will point to just after the return below, where we keep the thread
	literal patch-high-level			assemble
	literal return						assemble
end-fn immediate

fn: macro:
	\ value --
	high-level:
	assemble
	does> load assemble
end-fn

ptr-invoke-constant	macro: assemble-invoke-constant
ptr-invoke-variable	macro: assemble-invoke-variable
ptr-invoke-string	macro: assemble-invoke-string
find: literal		macro: assemble-literal
find: branch		macro: assemble-branch
find: jump			macro: assemble-jump

\ value --
\
\ `const:` merely defines a word with a reference to `invoke_constant` in the kernel, and appends the actual constant
\ value from the data stack
fn: const:
	define:
	assemble-invoke-constant assemble
end-fn

\ And here we see an example use of `const:` to define boolean constants
0 const: false
-1 const: true

\ -- patch-addr
\
\ One of the neat bits of TILs is that control flow constructs are themselves just words, and can be defined as such.
\ To function, they are tagged with `immediate`, which causes them to be executed at definition time when used, and as
\ such are able to modify the in-progress definition.
\
\ `if` works by appending the `~ branch <target>` sequence to the definition, leaving the address of `<target>` on the
\ stack to be patched by the corresponding `end-if`. As such, the final, assembled definition will branch past the
\ wrapped code if the top of the stack is zero, and fall through otherwise.
fn: if
	[ assemble-literal ] ~ assemble
	assemble-branch
	assembly-arena-ptr
	0 assemble
end-fn immediate

\ patch-addr --
\
\ `end-if` expects to find the patch address left by the corresponding `if` on the stack, and uses it compute and patch
\ in the true branch target.
fn: end-if
	assembly-arena-ptr over cell-size + - swap store
end-fn immediate

\ patch-addr -- new-patch-addr
\
\ This one's a bit tricky: the idea is to put an uncoditional jump at the end of the preceding if-block, end the
\ if-block, and leaving the address of the jump offset as the new patch address. This way, when `end-if` is executed, it
\ will patch this uncoditional jump, so that upon executing the if-block, execution will jump to just after the
\ `end-if`, and not fall through into the else-block.
fn: else
	assemble-jump
	assembly-arena-ptr 0 assemble
	swap assemble: end-if
end-fn immediate

\ -- target
\
\ For a `while <condition> do <body> end-while` construct, the underlying code is essentially the condition, followed by
\ an `if`-block guarding the loop body and an additional unconditional jump back to the condition. As such, the only
\ purpose of the `while` word is to record the address of the condition, so that the `end-while` word can assemble the
\ jump back to it.
fn: while assembly-arena-ptr end-fn immediate

\ -- patch-addr
\
\ As discussed with `while`, the responsibility of `do` is to begin the underlying `if`-block guarding the loop body
fn: do assemble: if end-fn immediate

\ target patch-addr --
\
\ Finally, `end-while` assembles the jump back to the condition, while also ending the `if`-block guarding the loop
\ body. A `swap` is needed since at the time it is executed, it expects to see the patch address of the preceding `do`
\ at the top of the stack, but it needs to assemble the backwards jump first, not perform the patch.
fn: end-while
	assemble-jump

	swap
	assembly-arena-ptr cell-size +
	- assemble

	assemble: end-if
end-fn immediate

\ ptr -- ptr in-bounds?
fn: in-line-bounds?
	copy line-start load -
	line-size load <
end-fn

\ -- next-ptr
fn: after-word get-current-word + end-fn

\ end-ptr --
fn: skip-line-segment
	current-word load - 1+		\ Length of the skipped portion
	current-word store-2nd		\ Store the length; the next accept-word will start at the end of the comment
end-fn

\ -- code
\
\ Pushes the first character after the first space character, advancing the line pointer to just after it
fn: char:
	after-word 1+ in-line-bounds?
	if
		copy load-byte push
		1+ skip-line-segment
		pop
	else
		drop 0 flush-line
	end-if
end-fn

\ n-cells --
fn: var:
	define:
	assemble-invoke-variable
	cell-size * assembly-arena-allocate
end-fn

1 var: delimiter-char

\ ( start-ptr char-code -- end-ptr )
fn: next-delimiter
	delimiter-char store
	while
		in-line-bounds?
		over load-byte delimiter-char load ~=
		&
	do
		1+
	end-while
end-fn

char: ) const: char-rparen

\ --
fn: (
	true
	while do
		after-word char-rparen next-delimiter
		in-line-bounds?
		if
			skip-line-segment false
		else
			drop repl-accept-line ~
		end-if
	end-while
end-fn immediate

fn: recurse ( -- ) current-definition load entry-data-ptr assemble end-fn immediate

fn: commit-string
	( string length -- committed-string length )

	copy 0~=
	if
		copy push
		copy assemble-byte
		assembly-arena-ptr push
		assemble-string
		0 assemble-byte
		pop pop
	else
		drop-pair
		\ Deep magic; I encourage you to think about it
		[
			assemble-literal
			0 assemble

			assembly-arena-ptr cell-size -
			assemble-literal
			assemble
		] swap
	end-if
end-fn

char: " const: dquote-char

\ Fails if there is no terminating `"` before the end of the line. Please note that it ignores the initial space.
fn: s"
	( -- string? length )

	after-word 1+
	copy dquote-char next-delimiter

	\ By this point we only have a pointer on the stack, which either points to the end of the line, or the `"`
	in-line-bounds?
	if
		copy skip-line-segment
		assembly-arena-start-block 	\ FIXME: this memory is leaked unless during compilation; we are fine with
									\ ovewriting the string during interpretation
		over - commit-string
	else
		\ If we didn't find the closing `"` before the end of the line, we need to discard the string
		drop-pair flush-line 0 0
	end-if
end-fn

fn: execute-handle
	( handle -- )
	
	is-initializing load
	if
		status-bad-execute print-line
		hard-fault
	end-if

	read-whole-file drop source-push-buffer
end-fn

\ `execute` is how we do raw interpretation of an on-disk script; the
\ high-level flow is that the file is opened, read into memory, null-terminated, then pushed onto the source context
\ stack. As soon as `execute` returns to the interpreter (note that this means it will behave very strangely within
\ a definition), the interpreter will continue reading from the in-memory source. When it reaches EOF, it pops the
\ source context, restoring the original one, with the rest of the line after the `execute` still intact.
fn: execute
	( string length )
	
	copy-pair push push
	drop open-file copy 0=
	if
		status-script-not-found print
		pop pop print-line nl
		drop soft-fault
	end-if

	pop pop drop-pair
	copy push
	execute-handle
	pop close-handle
end-fn

fn: execute" ( -- ) s" execute end-fn
fn: version ( -- ) version-string print-line end-fn

fn: string:
	( string length -- )

	define:
	assemble-invoke-string
	commit-string drop-pair
end-fn

\ Both of these contain tab characters
s" 	" string: empty-tag
s" *	" string: immediate-tag

char: 0						const:	digit-0
char: a						const:	digit-a
8 4 *						const:	formatted-decimal-size
formatted-decimal-size 8 /	var:	formatted-decimal
s" 0x"						string:	hex-prefix

1 var: use-hex
false use-hex store

fn: hex
	\ --
	true use-hex store
end-fn

1 var: base

fn: map-digit
	\ n -- c

	use-hex load
	if
		copy 10 < if
			digit-0 +
		else
			10 - digit-a +
		end-if
	else
		digit-0 +
	end-if
end-fn

fn: print-u#
	( n -- )

	use-hex load
	if
		16 base store
		hex-prefix print
	else
		10 base store
	end-if

	copy 0=
	if
		drop
		digit-0 formatted-decimal store-byte
		formatted-decimal 1 print
	else
		formatted-decimal
		formatted-decimal-size +
		copy push push

		while
			copy 0~=
		do
			copy base load u%
			map-digit pop 1 - swap over store-byte push
			base load u/
		end-while

		drop
		pop
		pop
		over - print
	end-if

	false use-hex store \ By clearing each time we can do things like `hex print-#`
end-fn

fn: print-#
	( n -- )

	copy 0>
	if
		negative print
		0- print-u#
	else
		print-u#
	end-if
end-fn

fn: cell-1+ ( cell-ptr -- ) copy load 1+ swap store end-fn

fn: checkpoint:
	( -- )

	high-level:
	dictionary load assemble
	does> load
end-fn

checkpoint: core-dict

version nl

(
	I seem to recall that I didn't like how namespacing [specifically things like namespace imports or private symbols]
	require some pretty invasive modifications to the interpreter, since it can no longer be done by simple
	modification of a single linked list, but rather requires a list of wordlists.
)

\ TODO: We must add wordlists next
\ TODO: and imports, or at least a C-style concept of local include directory (working dir for the interpreter) and
\ 	include guards
\ TODO: Or a test suite; use UnhandledExceptionFilter for reporting crashes
