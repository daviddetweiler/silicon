1 var: queried-word

fn: reverse-find
    ( word -- name? length? )

	queried-word store
	dictionary load

	while
		copy 0~=
	do
		copy entry-data-ptr
		queried-word load
		= if
			entry-name return
		end-if

		load
	end-while

	drop 0 0
end-fn

fn: show-pointer
    ( ptr-value -- )

    copy reverse-find
    copy 0~= if
        print-yellow nl drop
    else
        drop-pair hex print-u# nl
    end-if
end-fn

fn: show-instr
    ( thread-ptr -- )

    copy hex print-u# tabstop
    load show-pointer
end-fn

find: return const: return-word

s" Quoted word not found" string: not-found

fn: quote:
    ( -- )

    find:
    copy 0= if
        not-found print-red nl
        drop
        soft-fault
    end-if

    assemble-literal
    assemble
end-fn immediate

s" Not a threaded word" string: not-threaded

fn: is-final
    ( thread-ptr -- final? )
    
    load
    copy return-word = push
    copy quote: soft-fault = push
    quote: hard-fault =
    pop pop | |
end-fn

fn: disasm-raw
    ( thread-ptr -- )

    while
        copy is-final ~
    do
        copy show-instr
        cell-size +
    end-while

    show-instr
end-fn

fn: disasm-word
    ( word -- )

    copy load raw-invoke-thread ~= if
        drop
        not-threaded print-red nl
        return
    end-if

    cell-size + disasm-raw
end-fn

fn: disasm:
    ( -- )
    find:

    copy 0= if
        not-found print-red nl
        drop
        return
    end-if

    disasm-word
end-fn
